options 
{
    NODE_DEFAULT_VOID = true;
    LOOKAHEAD = 1;
}

PARSER_BEGIN(Parser)

public class Parser {
    public static void main(String[] args) throws ParseException {
        //Parser initialization
        parser.Parser parser;

        if (args.length != 1){
            System.out.println ("Usage: java Parser <file_directory>");
            return;
        }
        else {
            System.out.println("Parser: Reading file " + args[0] + " ..." );
            try {
                parser = new parser.Parser(new java.io.FileInputStream(args[0]));
            }
            catch(java.io.FileNotFoundException e) {
                System.out.println ("Parser: File " + args[0] + " not found.");
                return;
            }
        }

        try {            
            SimpleNode root = parser.Program();
            System.out.println ("Parser: File parsed successfully.");

            root.dump(""); // Prints syntax tree
        }
        catch(TokenMgrError e){
            System.out.println ("Parser: Token error.");
            System.out.println (e.getMessage());
        }  
        catch(ParseException e){
            System.out.println ("Parser: Parsing error.");            
            System.out.println (e.getMessage());
        }                 
    }
}

PARSER_END(Parser)

// Symbols that should be ignored
SKIP: {
    " " | "\n" | "\t" | "\r"
}

// Must ignore single and multi line comments
SKIP: {
    < "//" (~["\r", "\n"])* >
    | "/*" : ML_COMMENT_STATE
}

<ML_COMMENT_STATE> SKIP :
{
    "*/" : DEFAULT
    | < ~[] >   
}

// Definition of terminal symbols
TOKEN:
{
    < AND: "&&" >
    | < LOWER : "<" >
    | < PLUS : "+" > 
    | < MINUS : "-" >
    | < TIMES : "*" >
    | < DIVIDE : "/" >
    | < LENGTH : "length" >
    | < OPEN_PAREN : "(" >
    | < CLOSE_PAREN : ")" >
    | < COMMA : "," >
    | < DOT : "." >
    | < OPEN_BRACK : "[" >
    | < CLOSE_BRACK : "]" >
    | < VOID : "void" >
    | < INT : "int" >
    | < INTARRAY : "int[]" >
    | < STRINGARRAY : "String[]" >
    | < NOT : "!" >
    | < NEW :  "new" >
    | < THIS : "this" >
    | < TRUE : "true" >
    | < FALSE : "false" >
    | < ASSIGN : "=" >
    | < BOOLEAN : "boolean" >
    | < SEMI_COLON : ";" >
    | < WHILE : "while" >
    | < IF : "if" >
    | < ELSE : "else" >
    | < OPEN_BRACES : "{" >
    | < CLOSE_BRACES : "}" >
    | < PUBLIC : "public" >
    | < STATIC : "static" >
    | < MAIN : "main" >
    | < RETURN : "return" >
    | < EXTENDS : "extends" >
    | < CLASS : "class" >
    | < INTEGER_LITERAL : (["0"-"9"])+ >
    | < IDENTIFIER : (["a"-"z","A"-"Z"]) (["a"-"z","A"-"Z","0"-"9","_"])* > 
    
}

// Definition of Terminal Leafs
void Id() #ID: {Token t;}
{
    t=<IDENTIFIER> {jjtThis.value = t.image;}
}

void Integer() #INTEGER: {Token t;}
{
    t=<INTEGER_LITERAL> {jjtThis.value = t.image;}
}

void Type(): {}
{
    TypeNoId() | TypeId()
}

void TypeNoId(): {}
{
    <INTARRAY>#INTARRAY | <INT>#INT | <BOOLEAN>#BOOLEAN
}

void TypeId() #ID: {Token t;}
{
    t=<IDENTIFIER> {jjtThis.value = t.image;}
}

// Grammar Definition
SimpleNode Program() #PROGRAM: {}
{
    ClassDeclaration() <EOF> {return jjtThis;}
}

void ClassDeclaration() #CLASS: {}
{
    <CLASS>
    Id()
    (<EXTENDS> Id())? 
    <OPEN_BRACES> (VarDeclaration())* (MethodDeclaration())* <CLOSE_BRACES>
}

void VarDeclaration() #VAR: {}
{
    Type() Id() <SEMI_COLON>
}

void MethodDeclaration(): {}
{
    <PUBLIC> (
        (Type() Id() <OPEN_PAREN> Params() <CLOSE_PAREN> <OPEN_BRACES> Body() <CLOSE_BRACES>) #METHOD
        | (<STATIC> <VOID> <MAIN> <OPEN_PAREN> <STRINGARRAY> Id() <CLOSE_PAREN> <OPEN_BRACES> [MethodBody()] #BODY <CLOSE_BRACES>) #MAIN
    )
}

void Params() #PARAMETERS: {}
{
    (Type() Id() (<COMMA> Type() Id())*)?
}

void Body() #BODY: {}
{
    [MethodBody()] Return()
}

void Return() #RETURN: {}
{
    <RETURN> FullExpression() <SEMI_COLON>
}

void MethodBody(): {} 
{   
    VarDeclarationNoId() MethodBody()
    | Id() (Id() #VAR(2) <SEMI_COLON> MethodBody() | [IdStatement()] <SEMI_COLON> Statements())
    | NoIdStatement() Statements()
}

void VarDeclarationNoId() #VAR: {}
{
    TypeNoId() Id() <SEMI_COLON>
}

void Statements(): {}
{
    [Statement() Statements()]
}

void Statement(): {}
{
    NoIdStatement() | Id() [IdStatement()] <SEMI_COLON>
}

void IdStatement(): {} 
{   
    FullExpressionNoBrack()
    | <OPEN_BRACK> FullExpression() #INDEX(2) <CLOSE_BRACK> [(<ASSIGN> FullExpression() #ASSIGN(2) | FullExpressionNoFactor())]
    | <ASSIGN> FullExpression() #ASSIGN(2)
}

void NoIdStatement(): {boolean error = false;}
{   
    <OPEN_BRACES> Statements() <CLOSE_BRACES>
    | (<IF> <OPEN_PAREN> FullExpression() #COND <CLOSE_PAREN> Statement() #THEN <ELSE> Statement() #ELSE) #IF(3)
    | (
        <WHILE> 
        try {            
            <OPEN_PAREN> FullExpression() #COND <CLOSE_PAREN>
        }
        catch (ParseException e) {
            error = true;
            System.out.println ("Parser: Parsing error.");
            System.out.println(e.toString());  // Print the error message         
            error_skip();        
        }
        catch (TokenMgrError e) {
            error = true;
            System.out.println ("Parser: Token error.");
            System.out.println(e.toString());  // Print the error message
            error_skip();        
        }
        Statement() #DO
    ) #WHILE(error ? 1 : 2)
    | FullExpressionNoId() <SEMI_COLON>
}

void FullExpression(): {}           { CompExpression() (<AND> CompExpression() #AND(2))* }
void FullExpressionNoId(): {}       { CompExpressionNoId() (<AND> CompExpression() #AND(2))* }
void FullExpressionNoBrack(): {}    { (<AND> CompExpression() #AND(2) | CompExpressionNoBrack()) (<AND> CompExpression() #AND(2))* }
void FullExpressionNoFactor(): {}   { CompExpressionNoFactor() (<AND> CompExpression() #AND(2))* }

void CompExpression(): {}           { Expression() (<LOWER> Expression() #LOWER(2))* }
void CompExpressionNoId(): {}       { ExpressionNoId() (<LOWER> Expression() #LOWER(2))* }
void CompExpressionNoBrack(): {}    { (<LOWER> Expression() #LOWER(2) | ExpressionNoBrack()) (<LOWER> Expression() #LOWER(2))* }
void CompExpressionNoFactor(): {}   { ExpressionNoFactor() (<LOWER> Expression() #LOWER(2))* }

void Expression(): {}               { Term() (<PLUS> Term() #PLUS(2) | <MINUS> Term() #MINUS(2))* }
void ExpressionNoId(): {}           { TermNoId() (<PLUS> Term() #PLUS(2) | <MINUS> Term() #MINUS(2))* }
void ExpressionNoBrack(): {}        { ((<PLUS> Term() #PLUS(2) | <MINUS> Term() #MINUS(2)) | TermNoBrack()) 
                                      (<PLUS> Term() #PLUS(2) | <MINUS> Term() #MINUS(2))* }
void ExpressionNoFactor(): {}       { TermNoFactor() (<PLUS> Term() #PLUS(2) | <MINUS> Term() #MINUS(2))* }

void Term(): {}                 { Factor() (<TIMES> Factor() #TIMES(2) | <DIVIDE> Factor() #DIVIDE(2))* }
void TermNoId(): {}             { FactorNoId() (<TIMES> Factor() #TIMES(2) | <DIVIDE> Factor() #DIVIDE(2))* }
void TermNoBrack(): {}          { NoBrackNextFactor() (TermNoFactor() | (<TIMES> Factor() #TIMES(2) | <DIVIDE> Factor() #DIVIDE(2))*) }
void TermNoFactor(): {}         { (NextFactor())+ (<TIMES> Factor() #TIMES(2) | <DIVIDE> Factor() #DIVIDE(2))* }

void Factor(): {}               { (Id() | NoIdFactor()) NextFactors()}
void FactorNoId(): {}           { NoIdFactor() NextFactors() }

void NoIdFactor(): {}
{
    Integer()
    | <TRUE> #TRUE
    | <FALSE> #FALSE
    | <THIS> #THIS
    | <NEW> (<INT> <OPEN_BRACK> FullExpression() #NEWARRAY <CLOSE_BRACK> | Id() #NEWOBJ <OPEN_PAREN> <CLOSE_PAREN>)
    | <NOT> Factor() #NOT
    | <OPEN_PAREN> FullExpression() <CLOSE_PAREN>
}

void NextFactors(): {}
{
    NextFactor() NextFactors() | {}
}

void NextFactor(): {}
{
    NoBrackNextFactor()
    | <OPEN_BRACK> FullExpression() #INDEX(2) <CLOSE_BRACK>
}

void NoBrackNextFactor(): {}
{
    <DOT> (<LENGTH> #LENGTH(1) | Id() <OPEN_PAREN> FunctionCallParams() <CLOSE_PAREN> #FCALL(3))
}

void FunctionCallParams() #PARAMETERS: {}
{
    [FullExpression() (<COMMA> FullExpression())*]
}

JAVACODE
void error_skip() {
    Token t = getToken(1);

    while(t.kind != CLOSE_PAREN && t.kind != OPEN_BRACES && t.kind != IF && t.kind != WHILE && t.kind != SEMI_COLON) {        
        t = getNextToken();    
    }

    if(t.kind == CLOSE_PAREN || t.kind == SEMI_COLON) {
        getNextToken(); 
        System.out.println("T-> " + t.kind);
    }        
}